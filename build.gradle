import java.text.SimpleDateFormat

allprojects {
    group = 'com.github.b0ch3nski'
    version = new SimpleDateFormat("yyyyMMddhhmmss").format(new Date())

    if (project.hasProperty('build.type') && project.property('build.type') == 'release') {
        version = file("${rootDir}/VERSION").text.trim()
    }
}

subprojects {
    apply plugin: 'project-report'
    apply from: "${rootDir}/libraries.gradle"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    plugins.withType(JavaPlugin) {
        tasks.withType(JavaCompile) {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
            options.fork = true
            options.encoding = 'UTF-8'
            options.compilerArgs \
                << "-Xlint:all" \
                << "-Werror"
        }

        test {
            testLogging {
                events "failed"
                showExceptions true
                exceptionFormat "full"
                showCauses true
                showStackTraces true
                showStandardStreams true
            }
        }

        jar {
            baseName = rootProject.name + '-' + project.name
        }

        dependencies {
            compileOnly libraries.slf4j

            testCompile libraries.logback
            testCompile libraries.junit
            testCompile libraries.assertj
        }
    }
}
